import static org.gradle.api.tasks.wrapper.Wrapper.DistributionType

buildscript {
    ext {
        artifactoryContextUrl = "https://artifactory.corp.alleninstitute.org/artifactory"
    }
    repositories {
        mavenLocal()
        maven {
            url "${artifactoryContextUrl}/maven-virtual"
        }
        mavenCentral()
    }
    dependencies {
        classpath("org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1")
        classpath("org.alleninstitute.aics:gradle-plugins:1.0.23")
    }
}

plugins {
    id "com.dorongold.task-tree" version "1.3.1"
    id "com.moowork.node" version "1.2.0"
}

apply plugin: "maven-publish"
apply plugin: "maven-publish-auth"
apply plugin: "org.alleninstitute.git-info"

node {
    version = "10.15.3"
    npmVersion = "6.9.0"
    download = true
    nodeModulesDir = file("${project.projectDir}")
}

project.version = "0.1.x"
project.group = "org.alleninstitute.aics"

ext {
    // Repo settings
    artifactoryBaseUrl = "https://artifactory.corp.alleninstitute.org/artifactory"

    // TGZ Publishing
    rawArtifact = file("$projectDir/dist/file-upload-app 1.0.0.AppImage")
    artifactVersion = gitInfo.branch != "master" ? "${gitInfo.branch.tokenize("/").join("-")}" : project.version
    artifactName = "${project.name}-${ext.artifactVersion}.tgz"
}

wrapper {
    gradleVersion "5.2"
    setDistributionType(DistributionType.BIN)
}

task lint(type: YarnTask) {
    FileTree tsFiles = fileTree(dir: "src", includes: ["**/*.ts", "**/*.tsx"])
    args = ["tslint", "-c", "tslint.json"] + tsFiles.asList()
}

task test(type: YarnTask) {
    environment = [TS_NODE_PROJECT: "tsconfig.commonjs.json", NODE_ENV: "production"]
    args = ["mocha", "--opts", "mocha.opts", "src/**/test/*.{ts,tsx}"]
}

task dev(type: YarnTask) {
    environment = [
            NODE_ENV: "development",
    ]
    args = ["electron-webpack", "dev"]
}

///////////////////////////
// Build and publish
//////////////////////////

task compile(type: YarnTask) {
    args = ["electron-webpack"]
}

task bundle(type: YarnTask) {
    dependsOn compile
    args = ["electron-builder"]
}

/// Publishing
task packageForPublish(type: Tar) {
    dependsOn bundle

    from rawArtifact
    destinationDir file("${projectDir}")
    archiveName "$artifactName"
    compression Compression.GZIP
}

task artifactClean(type: Delete) {
    delete "$artifactName"
}

task localPublish {
    description "Create development module and publish to local cache"
    group "AICS Publishing"
    dependsOn packageForPublish, "publishMvnModulePublicationToMavenLocal"
}

task snapshotPublish {
    description "Create production module and publish to snapshot repo in artifactory"
    group "AICS Publishing"
    dependsOn packageForPublish, "publishMvnModulePublicationToSnapshotsRepoRepository"
}

publishing {
    publications {
        mvnModule(MavenPublication) {
            version artifactVersion
            artifact packageForPublish
        }
    }

    repositories {
        maven {
            name = "snapshotsRepo"
            url "${artifactoryBaseUrl}/maven-snapshot-local"
        }
    }
}
