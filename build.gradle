import static org.gradle.api.tasks.wrapper.Wrapper.DistributionType

buildscript {
    ext {
        artifactoryContextUrl = "https://artifactory.corp.alleninstitute.org/artifactory"
    }
    repositories {
        mavenLocal()
        maven {
            url "${artifactoryContextUrl}/maven-virtual"
        }
        mavenCentral()
    }
    dependencies {
        classpath("org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1")
        classpath("org.alleninstitute.aics:gradle-plugins:1.0.23")
    }
}

plugins {
    id "com.dorongold.task-tree" version "1.3.1"
    id "com.moowork.node" version "1.2.0"
}

apply plugin: "maven-publish"
apply plugin: "maven-publish-auth"
apply plugin: "org.alleninstitute.git-info"

node {
    version = "10.15.3"
    npmVersion = "6.9.0"
    download = true
    nodeModulesDir = file("${project.projectDir}")
}

project.version = "0.1.x"
project.group = "org.alleninstitute.aics"

ext {
    // Repo settings
    artifactoryBaseUrl = "https://artifactory.corp.alleninstitute.org/artifactory"

    // TGZ Publishing
    linuxArtifact = "file-upload-app 1.0.0.AppImage"
    windowsArtifact = "file-upload-app Setup 1.0.0.exe"

    artifactVersion = gitInfo.branch != "master" ? "${gitInfo.branch.tokenize("/").join("-")}" : project.version
    dockerImageName = "electron-app-builder:latest"
    dockerContext = "docker/"
}

wrapper {
    gradleVersion "5.2"
    setDistributionType(DistributionType.BIN)
}

task lint(type: YarnTask) {
    FileTree tsFiles = fileTree(dir: "src", includes: ["**/*.ts", "**/*.tsx"])
    args = ["tslint", "-c", "tslint.json"] + tsFiles.asList()
}

task test(type: YarnTask) {
    environment = [TS_NODE_PROJECT: "tsconfig.commonjs.json", NODE_ENV: "production"]
    args = ["mocha", "--opts", "mocha.opts", "src/**/test/*.{ts,tsx}"]
}

task dev(type: YarnTask) {
    environment = [NODE_ENV: "development"]
    args = ["electron-webpack", "dev"]
}

///////////////////////////
// Build and publish
//////////////////////////

task compile(type: YarnTask) {
    args = ["electron-webpack"]
}

task createDockerScripts(type: Copy) {
    def username = System.properties['user.name']
    def groupname = System.properties['group.name']
    def uid = ["id", "-u", username].execute().text.trim()
    def gid = ["id", "-g", username].execute().text.trim()

    from "docker-templates"
    into "${projectDir}/${dockerContext}"
    rename { file -> file - ".gradle_template"}
    expand([
            USERNAME: username,
            GROUPNAME: groupname,
            UID: uid,
            GID: gid
    ])
    outputs.upToDateWhen { false }
}

task dockerBuild(type: Exec) {
    dependsOn createDockerScripts
    commandLine "docker", "build", "-t", dockerImageName, dockerContext
}

def createBundleTask(nodeEnv, host, port, taskPrefix) {
    def args = [
            "docker", "run", "--rm",
            "--env", "ELECTRON_CACHE=/root/.cache/electron",
            "--env", "ELECTRON_BUILDER_CACHE=/root/.cache/electron-builder",
            "--env", "LIMS_PROTOCOL=http",
            "--env", "LIMS_HOST=$host",
            "--env", "LIMS_PORT=$port",
            "--env", "NODE_ENV=$nodeEnv",
            "-v", "${projectDir}:/project",
            "-v", "${projectDir}/node_modules:/project/node_modules",
            dockerImageName,
            "/bin/bash", "-c", "yarn dist-all"
    ]

    return tasks.create("${taskPrefix}Bundle", Exec) {
        dependsOn compile, dockerBuild
        commandLine args
    }
}

createBundleTask("development", "localhost", "8080", "dev")
createBundleTask("production", "stg-aics.corp.alleninstitute.org", "80", "stage")
createBundleTask("production", "aics.corp.alleninstitute.org", "80", "prod")

/// Publishing

task copyLinuxArtifact(type:Copy) {
    dependsOn prodBundle
    from "$projectDir/dist/$linuxArtifact"
    into projectDir
}

task copyWindowsArtifact(type:Copy) {
    dependsOn prodBundle
    from "$projectDir/dist/$windowsArtifact"
    into projectDir
}

task artifactClean(type: Delete) {
    delete "$projectDir/$linuxArtifact", "$projectDir/$windowsArtifact"
}

task localPublish {
    description "Create development module and publish to local cache"
    group "AICS Publishing"
    dependsOn copyLinuxArtifact, copyWindowsArtifact, "publishMvnModulePublicationToMavenLocal"
}

task snapshotPublish {
    description "Create production module and publish to snapshot repo in artifactory"
    group "AICS Publishing"
    dependsOn copyLinuxArtifact, copyWindowsArtifact, "publishMvnModulePublicationToSnapshotsRepoRepository"
}

publishing {
    publications {
        mvnModule(MavenPublication) {
            artifact(linuxArtifact) {
                classifier "file-upload-app 1.0.0"
                extension "AppImage"
                version artifactVersion
            }
            artifact(windowsArtifact) {
                classifier "file-upload-app Setup 1.0.0"
                extension "exe"
                version artifactVersion
            }
        }
    }

    repositories {
        maven {
            name = "snapshotsRepo"
            url "${artifactoryBaseUrl}/maven-snapshot-local"
        }
    }
}
